main :: () {
    gpu_init();

    main_queue := gpu_get_queue(.MAIN, 0);

    v_success, vertex_spv := compile_shader("../shaders/basic_vs.slang");
    assert(v_success);
    p_success, pixel_spv := compile_shader("../shaders/basic_ps.slang");
    assert(p_success);


    blend_state: Gpu_Blend_Desc;
    raster_desc := Gpu_Raster_Desc.{
        cull = .CW,
        color_targets = .[
            .{format = .R8G8B8A8_UNORM}
        ],
        blend_state = *blend_state,
    };

    graphics_pipeline := gpu_create_graphics_pipeline(vertex_spv, pixel_spv, raster_desc);

    texture_desc := Gpu_Texture_Desc.{
        format = .R8G8B8A8_UNORM,
        dimensions = .[1024, 1024, 1],
        usage = .COLOR_ATTACHMENT
    };
    texture := gpu_create_texture(texture_desc);
    view := gpu_texture_view(texture);

    Vertex2D :: struct {
        x: float;
        y: float;
    }

    gpu_arena := gpu_make_arena(1024);

    vertex_buffer, vertex_gpu := gpu_arena_alloc(*gpu_arena, [4] Vertex2D);
    vertex_buffer.* = .[
        .{0.5, 0.5},
        .{0.5, -0.5},
        .{-0.5, -0.5},
        .{-0.5, +0.5},
    ];

    vertex_colors, colors_gpu := gpu_arena_alloc(*gpu_arena, [12] float);
    vertex_colors.* = .[
        1., 0., 0.,
        0., 1., 0.,
        0., 0., 1.,
        1., 1., 1.,
    ];

    param_block, vertex_params_gpu := gpu_arena_alloc(*gpu_arena, [2] Gpu_Ptr);
    param_block.* = .[vertex_gpu, colors_gpu];

    index_buffer, index_gpu := gpu_arena_alloc(*gpu_arena, [6] u32);
    index_buffer.* = .[
        0, 3, 1,
        1, 3, 2,
    ];

    pixel_data, pixel_gpu := gpu_arena_alloc(*gpu_arena, [3] float);
    pixel_data.* = .[1., 0., 1.];

    gpu_capture_start();

    cmd_buff := gpu_start_command_recording(main_queue);
    {
        render_desc := Gpu_Render_Pass_Desc.{
            color_targets = .[
                .{
                    view = view,
                    load_op = .CLEAR,
                    store_op = .STORE,
                    clear_color = .[0, 0, 0, 1],
                }
            ]
        };
        gpu_begin_render_pass(cmd_buff, render_desc);

        gpu_set_pipeline(cmd_buff, graphics_pipeline);
        gpu_draw_indexed_instanced(cmd_buff, vertex_params_gpu, pixel_gpu, index_gpu, 6, 1);

        gpu_end_render_pass(cmd_buff);
    }

    gpu_submit(main_queue, cmd_buff);

    gpu_wait_idle();

    gpu_capture_end();

    gpu_free_arena(gpu_arena);
    gpu_free_pipeline(graphics_pipeline);
    gpu_free(texture);

    gpu_shutdown();
}

#import,file "../../simp_gpu/module.jai"(ENABLE_RENDER_CAPTURE = true);
#import "File";
#import "Basic";